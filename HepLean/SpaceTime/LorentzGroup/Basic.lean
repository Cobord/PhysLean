/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.SpaceTime.MinkowskiMetric
import HepLean.SpaceTime.LorentzVector.NormOne
/-!
# The Lorentz Group

We define the Lorentz group.

## References

- http://home.ku.edu.tr/~amostafazadeh/phys517_518/phys517_2016f/Handouts/A_Jaffi_Lorentz_Group.pdf

-/
/-! TODO: Show that the Lorentz is a Lie group. -/

noncomputable section

open Matrix
open Complex
open ComplexConjugate

/-!
## Matrices which preserves the Minkowski metric

We start studying the properties of matrices which preserve `Œ∑Lin`.
These matrices form the Lorentz group, which we will define in the next section at `lorentzGroup`.

-/
variable {d : ‚Ñï}

open minkowskiMetric in
/-- The Lorentz group is the subset of matrices which preserve the minkowski metric. -/
def LorentzGroup (d : ‚Ñï) : Set (Matrix (Fin 1 ‚äï Fin d) (Fin 1 ‚äï Fin d) ‚Ñù) :=
    {Œõ : Matrix (Fin 1 ‚äï Fin d) (Fin 1 ‚äï Fin d) ‚Ñù |
     ‚àÄ (x y : LorentzVector d), ‚ü™Œõ *·µ• x, Œõ *·µ• y‚ü´‚Çò = ‚ü™x, y‚ü´‚Çò}

namespace LorentzGroup
/-- Notation for the Lorentz group. -/
scoped[LorentzGroup] notation (name := lorentzGroup_notation) "ùìõ" => LorentzGroup

open minkowskiMetric

variable {Œõ Œõ' : Matrix (Fin 1 ‚äï Fin d) (Fin 1 ‚äï Fin d) ‚Ñù}

/-!

# Membership conditions

-/

lemma mem_iff_norm : Œõ ‚àà LorentzGroup d ‚Üî
    ‚àÄ (x : LorentzVector d), ‚ü™Œõ *·µ• x, Œõ *·µ• x‚ü´‚Çò = ‚ü™x, x‚ü´‚Çò := by
  refine Iff.intro (fun h x => h x x) (fun h x y => ?_)
  have hp := h (x + y)
  have hn := h (x - y)
  rw [mulVec_add] at hp
  rw [mulVec_sub] at hn
  simp only [map_add, LinearMap.add_apply, map_sub, LinearMap.sub_apply] at hp hn
  rw [symm (Œõ *·µ• y) (Œõ *·µ• x), symm y x] at hp hn
  linear_combination hp / 4 + -1 * hn / 4

lemma mem_iff_on_right : Œõ ‚àà LorentzGroup d ‚Üî
    ‚àÄ (x y : LorentzVector d), ‚ü™x, (dual Œõ * Œõ) *·µ• y‚ü´‚Çò = ‚ü™x, y‚ü´‚Çò := by
  apply Iff.intro
  intro h x y
  have h1 := h x y
  rw [‚Üê dual_mulVec_right, mulVec_mulVec] at h1
  exact h1
  intro h x y
  rw [‚Üê dual_mulVec_right, mulVec_mulVec]
  exact h x y

lemma mem_iff_dual_mul_self : Œõ ‚àà LorentzGroup d ‚Üî dual Œõ * Œõ = 1 := by
  rw [mem_iff_on_right, matrix_eq_id_iff]
  exact forall_comm

lemma mem_iff_self_mul_dual : Œõ ‚àà LorentzGroup d ‚Üî Œõ * dual Œõ = 1 := by
  rw [mem_iff_dual_mul_self]
  exact mul_eq_one_comm

lemma mem_iff_transpose : Œõ ‚àà LorentzGroup d ‚Üî Œõ·µÄ ‚àà LorentzGroup d := by
  apply Iff.intro
  ¬∑ intro h
    have h1 := congrArg transpose ((mem_iff_dual_mul_self).mp h)
    rw [dual, transpose_mul, transpose_mul, transpose_mul, minkowskiMatrix.eq_transpose,
      ‚Üê mul_assoc, transpose_one] at h1
    rw [mem_iff_self_mul_dual, ‚Üê h1, dual]
    noncomm_ring
  ¬∑ intro h
    have h1 := congrArg transpose ((mem_iff_dual_mul_self).mp h)
    rw [dual, transpose_mul, transpose_mul, transpose_mul, minkowskiMatrix.eq_transpose,
      ‚Üê mul_assoc, transpose_one, transpose_transpose] at h1
    rw [mem_iff_self_mul_dual, ‚Üê h1, dual]
    noncomm_ring

lemma mem_mul (hŒõ : Œõ ‚àà LorentzGroup d) (hŒõ' : Œõ' ‚àà LorentzGroup d) : Œõ * Œõ' ‚àà LorentzGroup d := by
  rw [mem_iff_dual_mul_self, dual_mul]
  trans dual Œõ' * (dual Œõ * Œõ) * Œõ'
  noncomm_ring
  rw [(mem_iff_dual_mul_self).mp hŒõ]
  simp [(mem_iff_dual_mul_self).mp hŒõ']

lemma one_mem : 1 ‚àà LorentzGroup d := by
  rw [mem_iff_dual_mul_self]
  simp

lemma dual_mem (h : Œõ ‚àà LorentzGroup d) : dual Œõ ‚àà LorentzGroup d := by
  rw [mem_iff_dual_mul_self, dual_dual]
  exact mem_iff_self_mul_dual.mp h

end LorentzGroup

/-!

# The Lorentz group as a group

-/

@[simps mul_coe one_coe inv div]
instance lorentzGroupIsGroup : Group (LorentzGroup d) where
  mul A B := ‚ü®A.1 * B.1, LorentzGroup.mem_mul A.2 B.2‚ü©
  mul_assoc A B C := by
    apply Subtype.eq
    exact Matrix.mul_assoc A.1 B.1 C.1
  one := ‚ü®1, LorentzGroup.one_mem‚ü©
  one_mul A := by
    apply Subtype.eq
    exact Matrix.one_mul A.1
  mul_one A := by
    apply Subtype.eq
    exact Matrix.mul_one A.1
  inv A := ‚ü®minkowskiMetric.dual A.1, LorentzGroup.dual_mem A.2‚ü©
  mul_left_inv A := by
    apply Subtype.eq
    exact LorentzGroup.mem_iff_dual_mul_self.mp A.2

/-- `LorentzGroup` has the subtype topology. -/
instance : TopologicalSpace (LorentzGroup d) := instTopologicalSpaceSubtype

namespace LorentzGroup

open minkowskiMetric

variable {Œõ Œõ' : LorentzGroup d}

lemma coe_inv : (Œõ‚Åª¬π).1 = Œõ.1‚Åª¬π:= by
  refine (inv_eq_left_inv ?h).symm
  exact mem_iff_dual_mul_self.mp Œõ.2

/-- The transpose of a matrix in the Lorentz group is an element of the Lorentz group. -/
def transpose (Œõ : LorentzGroup d) : LorentzGroup d :=
  ‚ü®Œõ.1·µÄ, mem_iff_transpose.mp Œõ.2‚ü©

/-!

## Lorentz group as a topological group

We now show that the Lorentz group is a topological group.
We do this by showing that the natrual map from the Lorentz group to `GL (Fin 4) ‚Ñù` is an
embedding.

-/

/-- The homomorphism of the Lorentz group into `GL (Fin 4) ‚Ñù`. -/
def toGL : LorentzGroup d ‚Üí* GL (Fin 1 ‚äï Fin d) ‚Ñù where
  toFun A := ‚ü®A.1, (A‚Åª¬π).1, mul_eq_one_comm.mpr $ mem_iff_dual_mul_self.mp A.2,
    mem_iff_dual_mul_self.mp A.2‚ü©
  map_one' := by
    simp
    rfl
  map_mul' x y := by
    simp only [lorentzGroupIsGroup, _root_.mul_inv_rev, coe_inv]
    ext
    rfl

lemma toGL_injective : Function.Injective (@toGL d) := by
  intro A B h
  apply Subtype.eq
  rw [@Units.ext_iff] at h
  exact h

/-- The homomorphism from the Lorentz Group into the monoid of matrices times the opposite of
  the monoid of matrices. -/
@[simps!]
def toProd : LorentzGroup d ‚Üí* (Matrix (Fin 1 ‚äï Fin d) (Fin 1 ‚äï Fin d) ‚Ñù) √ó
    (Matrix (Fin 1 ‚äï Fin d) (Fin 1 ‚äï Fin d) ‚Ñù)·µê·µí·µñ :=
  MonoidHom.comp (Units.embedProduct _) toGL

lemma toProd_eq_transpose_Œ∑ : toProd Œõ = (Œõ.1, MulOpposite.op $ minkowskiMetric.dual Œõ.1) := rfl

lemma toProd_injective : Function.Injective (@toProd d) := by
  intro A B h
  rw [toProd_eq_transpose_Œ∑, toProd_eq_transpose_Œ∑] at h
  rw [@Prod.mk.inj_iff] at h
  apply Subtype.eq
  exact h.1

lemma toProd_continuous : Continuous (@toProd d) := by
  change Continuous (fun A => (A.1, ‚ü®dual A.1‚ü©))
  refine continuous_prod_mk.mpr (And.intro ?_ ?_)
  exact continuous_iff_le_induced.mpr fun U a => a
  refine Continuous.comp' ?_ ?_
  exact MulOpposite.continuous_op
  refine Continuous.matrix_mul (Continuous.matrix_mul continuous_const ?_) continuous_const
  refine Continuous.matrix_transpose ?_
  exact continuous_iff_le_induced.mpr fun U a => a

/-- The embedding from the Lorentz Group into the monoid of matrices times the opposite of
  the monoid of matrices. -/
lemma toProd_embedding : Embedding (@toProd d) where
  inj := toProd_injective
  induced := by
    refine (inducing_iff ‚áëtoProd).mp ?_
    refine inducing_of_inducing_compose toProd_continuous continuous_fst ?hgf
    exact (inducing_iff (Prod.fst ‚àò ‚áëtoProd)).mpr rfl

/-- The embedding from the Lorentz Group into `GL (Fin 4) ‚Ñù`. -/
lemma toGL_embedding : Embedding (@toGL d).toFun where
  inj := toGL_injective
  induced := by
    refine ((fun {X} {t t'} => TopologicalSpace.ext_iff.mpr) ?_).symm
    intro s
    rw [TopologicalSpace.ext_iff.mp toProd_embedding.induced s ]
    rw [isOpen_induced_iff, isOpen_induced_iff]
    exact exists_exists_and_eq_and

instance : TopologicalGroup (LorentzGroup d) :=
Inducing.topologicalGroup toGL toGL_embedding.toInducing

section
open LorentzVector
/-!

# To a norm one Lorentz vector

-/

/-- The first column of a Lorentz matrix as a `NormOneLorentzVector`. -/
@[simps!]
def toNormOneLorentzVector (Œõ : LorentzGroup d) : NormOneLorentzVector d :=
  ‚ü®Œõ.1 *·µ• timeVec, by rw [NormOneLorentzVector.mem_iff, Œõ.2, minkowskiMetric.on_timeVec]‚ü©

/-!

# The time like element

-/

/-- The time like element of a Lorentz matrix. -/
@[simp]
def timeComp (Œõ : LorentzGroup d) : ‚Ñù := Œõ.1 (Sum.inl 0) (Sum.inl 0)

lemma timeComp_eq_toNormOneLorentzVector : timeComp Œõ = (toNormOneLorentzVector Œõ).1.time := by
  simp only [time, toNormOneLorentzVector, timeVec, Fin.isValue, timeComp]
  erw [Pi.basisFun_apply, mulVec_stdBasis]

lemma timeComp_mul (Œõ Œõ' : LorentzGroup d) : timeComp (Œõ * Œõ') =
    ‚ü™toNormOneLorentzVector (transpose Œõ), (toNormOneLorentzVector Œõ').1.spaceReflection‚ü´‚Çò := by
  simp only [timeComp, Fin.isValue, lorentzGroupIsGroup_mul_coe, mul_apply, Fintype.sum_sum_type,
    Finset.univ_unique, Fin.default_eq_zero, Finset.sum_singleton, toNormOneLorentzVector,
    transpose, timeVec, right_spaceReflection, time, space, PiLp.inner_apply, Function.comp_apply,
    RCLike.inner_apply, conj_trivial]
  erw [Pi.basisFun_apply, mulVec_stdBasis]
  simp

end
end LorentzGroup
